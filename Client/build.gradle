apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'


ext {
    compileSdkProjectVersion = 23
    buildToolsProjectVersion = '23.0.3'
    supportLibraryVersion = '23.3.0'
    versionProjectCode = 1
    versionProjectName = "0.1.0"
    baseScheme = 'http'
    baseHost = 'budclub.ru'
    requeryVersion = '1.1.0'
    daggerVersion = '2.4'
    lombokVersion = '1.16.4'
}

android {

    compileSdkVersion compileSdkProjectVersion
    buildToolsVersion buildToolsProjectVersion

    defaultConfig {
        applicationId "ru.samlib.client"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode versionProjectCode
        versionName versionProjectName
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding = 'UTF-8'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    applicationVariants.all { variant ->
        variant.buildConfigField "String", "BASE_SCHEME", "\"$baseScheme\""
        variant.buildConfigField "String", "BASE_HOST", "\"$baseHost\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            manifest.srcFile '/src/main/AndroidManifest.xml'
            java.srcDirs = ['/src/main/java', '.apt_generated']
            aidl.srcDirs = ['/src/main/aidl', '.apt_generated']
            assets.srcDirs = ['/src/main/assets']
            res.srcDirs =
                    [
                            'src/main/res/layouts/activity',
                            'src/main/res/layouts/fragment',
                            'src/main/res/layouts/item',
                            'src/main/res/layouts/view',
                            'src/main/res'
                    ]
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage', 'MissingPrefix', 'WrongConstant'
    }

}

retrolambda {
    jdk System.getenv("JAVA8_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods true
}

dependencies {
    compile fileTree(include: ["*.jar"], dir: "libs")
    apt "org.projectlombok:lombok:$lombokVersion"
    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    apt "com.jakewharton:butterknife:7.0.1"
    apt "io.requery:requery-processor:$requeryVersion"   // use an APT plugin
    provided "javax.annotation:jsr250-api:1.0"
    provided "org.projectlombok:lombok:$lombokVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:gridlayout-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:support-annotations:$supportLibraryVersion"

    compile "org.jsoup:jsoup:1.8.3"
    compile "com.annimon:stream:1.0.1"
    compile "com.jakewharton:butterknife:7.0.1"
    compile "de.greenrobot:eventbus:2.4.0"
    compile "com.squareup.picasso:picasso:2.5.2"
    compile "com.google.code.gson:gson:2.4"
    compile "uk.co.chrisjenx:calligraphy:2.1.0"
    compile "com.google.dagger:dagger:$daggerVersion"
    //   compile "com.github.User:Repo:Tag"  for github repos
    compile "com.github.NightWhistler:HtmlSpanner:0.4"
    compile "xyz.danoz:recyclerviewfastscroller:0.1.3"
    compile "com.snappydb:snappydb-lib:0.5.2"
    compile "io.reactivex:rxjava:1.1.5"
    compile "io.reactivex:rxandroid:1.1.0"
    compile "com.evernote:android-job:1.0.7"
    compile "net.vrallev.android:cat:1.0.3"

    compile "io.requery:requery:$requeryVersion"
    compile "io.requery:requery-android:$requeryVersion" // for android

}
